plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "pl.softfly.flashcards"
        testApplicationId "pl.softfly.flashcards.test"
        minSdkVersion 30
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def room_version = "2.3.0"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"

    /* Apache POI */
    implementation 'org.apache.commons:commons-compress:1.20'
    implementation 'org.apache.commons:commons-collections4:4.4'
    /* Caused by: java.lang.ClassNotFoundException: org.apache.commons.math3.util.ArithmeticUtils
     * I don't know why this relationship does not exist in POI */
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation files('../android-apache-poi-5/build/libs/poi-5.0.0-all.jar')

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'com.getbase:floatingactionbutton:1.10.1'

    testImplementation 'junit:junit:4.+'
    testImplementation 'org.mockito:mockito-all:1.10.19'
    testImplementation 'io.cucumber:cucumber-java:6.11.0'
    testImplementation 'org.assertj:assertj-core:3.21.0'


    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumber() {
    dependsOn assemble
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', '', 'src/test/resources']
        }
    }
}

